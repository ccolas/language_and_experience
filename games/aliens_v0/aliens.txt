BasicGame
    SpriteSet
        floor > Immovable img=colors/LIGHTGRAY
        wall > Immovable img=colors/DARKGRAY
        base    > Immovable img=colors/WHITE
        avatar  > FlakAvatar stype=sam img=colors/DARKBLUE
        sam > Missile orientation=UP img=colors/BLUE singleton=True
        bomb > Missile orientation=DOWN img=colors/RED speed=0.2
        alien > Bomber stype=bomb orientation=RIGHT prob=0.03  cooldown=3 speed=0.6 img=colors/PINK
        portalSlow  > SpawnPoint   stype=alien  cooldown=16   total=20 img=colors/ORANGE
        portalFast  > SpawnPoint   stype=alien  cooldown=12   total=20 img=colors/BROWN

    LevelMapping
        . > floor
        w > floor wall
        0 > floor base
        1 > floor portalSlow
        2 > floor portalFast
        A > floor avatar

    TerminationSet
        SpriteCounter      stype=avatar               limit=0 win=False
        MultiSpriteCounter stype1=portalSlow stype2=alien stype3=portalFast limit=0 win=True

    InteractionSet
        avatar  wall  > stepBack
        alien   wall  > turnAround
        sam wall  > killSprite
        bomb wall  > killSprite

        base bomb > killSprite
        bomb base > killSprite
        sam base > killSprite
        base sam > killSprite

        base   alien > killSprite
        avatar alien > killSprite scoreChange=-1
        avatar bomb  > killSprite scoreChange=-1
        alien  sam   > killSprite scoreChange=1
        
        alien EOS > killSprite
        avatar EOS > killSprite
        base EOS > killSprite
        bomb EOS > killSprite
        portalFast EOS > killSprite
        portalSlow EOS > killSprite
        sam EOS > killSprite
        wall EOS > killSprite
